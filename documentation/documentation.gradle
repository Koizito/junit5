import org.apache.tools.ant.taskdefs.condition.Os

buildscript {
	dependencies {
		// upgrade to latest jruby version due to a bugfix needed for Windows 10.
		// can be removed, when asciidoctorj uses this as a default version.
		classpath('org.jruby:jruby-complete:9.1.15.0')

		// classpath('org.asciidoctor:asciidoctorj-epub3:1.5.0-alpha.16')
		classpath('org.asciidoctor:asciidoctorj-pdf:1.5.0-alpha.16')
		classpath('org.asciidoctor:asciidoctorj-diagram:1.5.4.1')
	}
}

plugins {
	id 'org.asciidoctor.convert' version '1.5.7'
}

test {
	useJUnitPlatform {
		excludeTags 'exclude'
	}
	testLogging {
		showStandardStreams true

		afterTest { TestDescriptor td, TestResult tr ->
			out << 'TEST: ' + td.className + ' ' + td.name + ' ' + tr.resultType.name() + "\n"
			TestDescriptor parent = td.parent
			while (parent != null) {
				out << ' TEST(PARENT): ' + parent.name + '\n'
				parent = parent.parent
			}
			out << '\n'
		}
	}
	systemProperty 'java.util.logging.manager', 'org.apache.logging.log4j.jul.LogManager'
}

dependencies {
	asciidoctor('org.jruby:jruby-complete:9.1.15.0')

	testImplementation(project(':junit-jupiter-api'))
	testImplementation(project(path: ':junit-jupiter-params', configuration: 'shadow'))
	testImplementation(project(':junit-platform-runner'))
	testImplementation(project(':junit-platform-launcher'))
	if (gradle.ext.kotlinIsSupported) testImplementation("org.jetbrains.kotlin:kotlin-stdlib:${kotlinVersion}")

	// Needed for `generateConsoleLauncherOptions` task
	testRuntimeOnly(project(path: ':junit-platform-console', configuration: 'shadow'))

	testRuntimeOnly(project(':junit-vintage-engine'))
	testRuntimeOnly(project(':junit-jupiter-engine'))
	testRuntimeOnly("org.apache.logging.log4j:log4j-core:${log4jVersion}")
	testRuntimeOnly("org.apache.logging.log4j:log4j-jul:${log4jVersion}")
}

asciidoctorj {
	version = '1.5.6'
}

ext {
	generatedAsciiDocInputDir = buildDir.toPath().resolve('generated/asciidoc').toString()
}

task generateConsoleLauncherOptions(type: JavaExec){
	def outputPath = java.nio.file.Paths.get(generatedAsciiDocInputDir)
	def outputFile = outputPath.resolve('console-launcher-options.txt')
	classpath = configurations.testRuntimeClasspath
	main = 'org.junit.platform.console.ConsoleLauncher'
	args = ['--help']
	standardOutput = new ByteArrayOutputStream()
	doLast {
		java.nio.file.Files.createDirectories(outputPath)
		java.nio.file.Files.write(outputFile, standardOutput.toByteArray())
	}
}

asciidoctor {
	dependsOn generateConsoleLauncherOptions

	// enable the Asciidoctor Diagram extension
	requires 'asciidoctor-diagram'

	separateOutputDirs false
	sources {
		include '**/index.adoc'
	}
	resources {
		from(sourceDir) {
			include '**/images/**'
		}
	}

	backends 'html5'

	// The PDF backend currently does not work on Linux
	// see https://github.com/junit-team/junit5/issues/1099
	if (Os.isFamily(Os.FAMILY_WINDOWS) || Os.isFamily(Os.FAMILY_MAC)) {
		backends 'pdf'
	}

	attributes	'jupiter-version': project.property('version'),
				'platform-version': project.property('platformVersion'),
				'vintage-version': project.property('vintageVersion'),
				'junit4-version': project.property('junit4Version'),
				'apiguardian-version': project.property('apiGuardianVersion'),
				'ota4j-version': project.property('ota4jVersion'),
				'release-branch': project.property('releaseBranch'),
				'docs-version': project.property('docsVersion'),
				'revnumber' : project.version,
				'mainDir': project.sourceSets.main.java.srcDirs[0],
				'testDir': project.sourceSets.test.java.srcDirs[0],
				'generatedAsciiDocInputDir': generatedAsciiDocInputDir,
				'testResourcesDir': project.sourceSets.test.resources.srcDirs[0],
				'outdir': outputDir.absolutePath,
				'source-highlighter': 'coderay@', // TODO switch to 'rouge' once supported by the html5 backend and on MS Windows
				'tabsize': '4',
				'toc': 'left',
				'icons': 'font',
				'sectanchors': true,
				'idprefix': '',
				'idseparator': '-'
	if (gradle.ext.kotlinIsSupported) {
		attributes.put('kotlinTestDir', project.sourceSets.test.kotlin.srcDirs[0])
	}
}
